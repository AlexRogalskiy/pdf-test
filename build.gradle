apply plugin: 'java'
apply plugin: 'maven'

group='com.codeborne'
archivesBaseName = 'pdf-test'
version='1.5-SNAPSHOT'

[compileJava, compileTestJava]*.options.collect {options -> options.encoding = 'UTF-8'}
[compileJava, compileTestJava]*.options.collect {options -> options.debug = true}
compileJava.options.debugOptions.debugLevel = "source,lines,vars"

sourceCompatibility = 1.7
targetCompatibility = 1.7

defaultTasks 'test', 'install'

dependencies {
  compile group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.0', transitive: true
  compile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3', transitive: false
  testCompile group: 'junit', name: 'junit', version: '4.12', transitive: false
}

repositories{
  mavenCentral()
}

task libs(type: Sync) {
  from configurations.compile
  from configurations.testCompile
  into "$buildDir/lib"
}

compileJava.dependsOn libs

test {
  include 'com/codeborne/pdftest/**/*'
  systemProperties['file.encoding'] = 'UTF-8'
  testLogging.showStandardStreams = true
}

jar {
  manifest {
    attributes(
      "Implementation-Title": project.group + '.' + project.name,
      "Implementation-Version": version,
      "Implementation-Vendor": "Codeborne")
  }
}

task sourcesJar(type: Jar, dependsOn:classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

javadoc {
  failOnError=false
}

task javadocJar(type: Jar, dependsOn:javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.12'
  jarFile = './gradle-wrapper/gradle-wrapper.jar'
  scriptFile = './gradle'
}
